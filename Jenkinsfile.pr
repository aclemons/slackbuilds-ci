#!/usr/bin/env groovy

// Copyright 2023 Andrew Clemons, Tokyo Japan
// All rights reserved.
//
// Redistribution and use of this script, with or without modification, is
// permitted provided that the following conditions are met:
//
// 1. Redistributions of this script must retain the above copyright
//    notice, this list of conditions and the following disclaimer.
//
//  THIS SOFTWARE IS PROVIDED BY THE AUTHOR "AS IS" AND ANY EXPRESS OR IMPLIED
//  WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
//  MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.  IN NO
//  EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
//  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
//  PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
//  OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

def libraryPath = '.slackbuilds-library'

node('master') {
    echo "Loading local shared library"

    checkout poll: false, changelog: false, scm: scm

    // create new git repo inside jenkins subdirectory
    sh(
        """
          rm -rf sbo-maintainer-tools && \
          cd $libraryPath && \
          (rm -rf .git 2>&1 > /dev/null || true) && \
          git init && \
          git config --local user.email "sbo-bot@slackbuilds.org" && \
          git config --local user.name "sbo-bot" && \
          git add --all && \
          git commit -m init
        """
    )

    def repoPath = sh(returnStdout: true, script: 'pwd').trim() + "/$libraryPath"

    library(identifier: 'local-lib@master', retriever: modernSCM([$class: 'GitSCMSource', remote: "$repoPath"]), changelog: false)

    deleteDir() // After loading the library we can clean the workspace

    echo "Done loading shared library"
}

node('master') {
    stage('Prepare') {
        try {
            checkout(
                scmGit(
                    branches: [[name: "pull-$gh_pr"]],
                    extensions: [checkoutOption(), cloneOption(depth: 1, honorRefspec: true, noTags: true, reference: '', shallow: false), [$class: 'RelativeTargetDirectory', relativeTargetDir: 'slackbuilds']],
                    userRemoteConfigs: [[name: 'origin', refspec: "+refs/pull/$gh_pr/merge:refs/remotes/origin/pull-$gh_pr", url: "https://github.com/${repo}.git"]]
                )
            )
        } catch (err) {
            def text = "# buildpkg $build_arch $build_package\nCannot checkout PR.\n```\n${err.toString()}\n```\n"
            writeFile(file: 'checkout-error', text: text)
            withCredentials([usernamePassword(credentialsId: 'sbo-bot', passwordVariable: 'GITHUB_TOKEN', usernameVariable: 'IGNORED')]) {
                sh "gh pr comment --repo=${repo} $gh_pr --body-file checkout-error"
            }
            throw err
        }
    }

    stage('Build') {
        dir('slackbuilds') {
            withEnv(["build_arch=$build_arch", "build_package=$build_package", "action=$action"]) {
                sh(script: libraryResource('buildpkg'))
            }
        }
    }

    stage('Comment') {
        withCredentials([usernamePassword(credentialsId: 'sbo-bot', passwordVariable: 'GITHUB_TOKEN', usernameVariable: 'IGNORED')]) {
            sh "if [ -e build_results ] ; then gh pr comment --repo=${repo} $gh_pr --body-file build_results ; fi"
            sh "if [ -e lint_results ] ; then gh pr comment --repo=${repo} $gh_pr --body-file lint_results ; fi"
        }
    }
}
